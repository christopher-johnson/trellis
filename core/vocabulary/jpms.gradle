apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Vocabularies'

ext {
    javaModuleName = 'org.trellisldp.jpms.vocabulary'
    osgiModuleName = 'org.trellisldp.vocabulary'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['org.apache.jena.base','org.apache.jena.core', 'org.apache.jena.arq', 'jsonld.java']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")

    implementation("de.ubleipzig:jena.arq-jpms:$jenaVersion")
    implementation("de.ubleipzig:jena.base-jpms:$jenaVersion")
    implementation("de.ubleipzig:jena.core-jpms:$jenaVersion")

    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("com.google.guava:guava:$guavaVersion") {force = true}
    testImplementation("commons-codec:commons-codec:$commonsCodecVersion") {force = true}
    testImplementation("commons-io:commons-io:$commonsIoVersion") {force = true}
    testImplementation("com.github.jsonld-java:jsonld-java:$jsonldVersion") {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient-osgi'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore-osgi'
    }
    testImplementation("org.apache.commons:commons-rdf-jena-jpms:$commonsRdfJenaVersion")
    testImplementation("org.apache.httpcomponents:httpclient-cache:$httpclientVersion"){
        exclude(module: 'commons-logging')
    }
    testImplementation("de.ubleipzig:jena.iri-jpms:$jenaVersion")
    testImplementation("org.apache.thrift:libthrift:$thriftVersion"){
        exclude(module: 'httpclient')
        exclude(module: 'httpcore')
    }
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$osgiModuleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",osgi.serviceloader;
                filter:="(osgi.serviceloader=org.apache.commons.rdf.api.RDF)";resolution:=mandatory; cardinality:=at_least_one,osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'''
    }
    }
