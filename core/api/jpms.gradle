apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis API'

ext {
    javaModuleName = 'org.trellisldp.jpms.api'
    osgiModuleName = 'org.trellisldp.api'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['org.trellisldp.jpms.vocabulary', 'org.apache.jena.core', 'org.apache.jena.arq']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api("org.apache.commons:commons-lang3:$commonsLangVersion")

    testImplementation("org.trellisldp:trellis-vocabulary-jpms:$version")
    testImplementation("commons-io:commons-io:$commonsIoVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
    testImplementation("org.apache.commons:commons-rdf-jena-jpms:$commonsRdfJenaVersion")
    testImplementation("org.apache.commons:commons-text:$commonsTextVersion")
    testImplementation("org.apache.jena:jena.arq-jpms:$jenaVersion")
    testImplementation("org.apache.jena:jena.core-jpms:$jenaVersion")
    testImplementation("org.apache.jena:jena.iri-jpms:$jenaVersion")
    testImplementation("org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces:$servicemixXercesVersion") {
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    testImplementation("org.apache.thrift:libthrift:$thriftVersion"){
        exclude(module: 'httpclient')
        exclude(module: 'httpcore')
    }
    testImplementation("com.google.guava:guava:$guavaVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$osgiModuleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",osgi.serviceloader;
                filter:="(osgi.serviceloader=org.apache.commons.rdf.api.RDF)"; resolution:=mandatory; cardinality:=at_least_one,
                osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'''
    }
}

