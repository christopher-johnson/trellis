apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Constraint Rules'

ext {
    javaModuleName = 'org.trellisldp.jpms.constraint'
    osgiModuleName = 'org.trellisldp.constraint'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['org.mockito', 'org.apache.jena.arq', 'org.apache.jena.core']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api("org.trellisldp:trellis-api-jpms:$version")

    implementation("org.slf4j:slf4j-api:$slf4jVersion")
    implementation("org.trellisldp:trellis-vocabulary-jpms:$version")

    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("com.google.guava:guava:$guavaVersion") {force = true}
    testImplementation("commons-io:commons-io:$commonsIoVersion") { force = true }
    testImplementation("org.apache.commons:commons-rdf-jena-jpms:$commonsRdfJenaVersion")
    testImplementation("org.apache.httpcomponents:httpclient-cache:$httpclientVersion"){
        exclude(module: 'commons-logging')
    }
    testImplementation("org.apache.jena:jena.arq-jpms:$jenaVersion")
    testImplementation("org.apache.jena:jena.base-jpms:$jenaVersion")
    testImplementation("org.apache.jena:jena.core-jpms:$jenaVersion")
    testImplementation("org.apache.jena:jena.iri-jpms:$jenaVersion")
    testImplementation("org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces:$servicemixXercesVersion") {
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    testImplementation("org.apache.thrift:libthrift:$thriftVersion"){
        exclude(module: 'httpclient')
        exclude(module: 'httpcore')
    }
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$osgiModuleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''osgi.extender; filter:="(osgi.extender=osgi.serviceloader.registrar)";resolution:=optional, osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'''
        instruction 'Provide-Capability', '''osgi.serviceloader; osgi.serviceloader=org.trellisldp.api.ConstraintService'''
    }
}

