apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Constraint Rules'

ext {
    moduleName = 'org.trellisldp.jpms.constraint'
}

javaModule {
    name = moduleName
    extraTestModules = ['org.mockito','org.apache.jena.arq']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api project(':trellis-api')

    implementation("org.slf4j:slf4j-api:$slf4jVersion")
    implementation project(':trellis-vocabulary')

    testImplementation("commons-io:commons-io:$commonsIoVersion") {force = true}
    testImplementation("org.apache.jena:jena.arq-jpms:$jenaVersion")
    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("org.apache.commons:commons-rdf-jena-jpms:$commonsRdfJenaVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$moduleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''\
            osgi.extender;
                filter:="(osgi.extender=osgi.serviceloader.registrar)";
                resolution:=optional'''
        instruction 'Provide-Capability', '''\
            osgi.serviceloader;
                osgi.serviceloader=org.trellisldp.api.ConstraintService'''
    }
    appendix 'jpms'
}

