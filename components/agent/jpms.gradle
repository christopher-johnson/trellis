apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Agent'

ext {
    osgiModuleName = 'org.trellisldp.agent'
    javaModuleName = 'org.trellisldp.jpms.agent'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['org.trellisldp.jpms.vocabulary', 'org.apache.commons.rdf.api']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api project(':trellis-api')
    api project(':trellis-vocabulary')

    testImplementation project(':trellis-vocabulary')
    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$osgiModuleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''osgi.extender; filter:="(osgi.extender=osgi.serviceloader.registrar)"; 
                resolution:=optional, osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'''
        instruction 'Provide-Capability', '''osgi.serviceloader; osgi.serviceloader=org.trellisldp.api.AgentService'''
    }
    appendix 'jpms'
}

