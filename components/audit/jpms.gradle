apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Audit'

ext {
    javaModuleName = 'org.trellisldp.jpms.audit'
    osgiModuleName = 'org.trellisldp.audit'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['org.mockito']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api("org.trellisldp:trellis-api-jpms:$version")

    implementation("org.trellisldp:trellis-vocabulary-jpms:$version")

    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$osgiModuleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''osgi.extender; filter:="(osgi.extender=osgi.serviceloader.registrar)";resolution:=optional, osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'''
        instruction 'Provide-Capability', '''osgi.serviceloader;osgi.serviceloader=org.trellisldp.api.AuditService'''
    }
}

javadoc {
    options.tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
}

