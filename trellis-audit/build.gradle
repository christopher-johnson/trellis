apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Audit'

ext {
    moduleName = 'org.trellisldp.audit'
}

javaModule {
    name = 'org.trellisldp.audit'
    extraTestModules = ['org.mockito']
}

dependencies {
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api project(':trellis-api')

    implementation project(':trellis-vocabulary')

    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
        instruction 'Require-Capability', '''\
            osgi.extender;
                filter:="(osgi.extender=osgi.serviceloader.registrar)";
                resolution:=optional'''
        instruction 'Provide-Capability', '''\
            osgi.serviceloader;
                osgi.serviceloader=org.trellisldp.api.AuditService'''
    }
    appendix 'jpms'
}

javadoc {
    options.tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
}

