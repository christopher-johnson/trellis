buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.1'
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.zyxist.chainsaw' version '0.3.1'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'org.sonarqube' version '2.6.2'
    id 'com.gradle.build-scan' version '1.12.1'
    id 'nebula.ospackage' version '4.8.0'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

ext {

    /* Dependencies */
    activationApiVersion = '1.2.0'
    activeMqVersion = '5.15.3'
    bouncyCastleVersion = '1.59'
    commonsCodecVersion = '1.11'
    commonsCollectionsVersion = '4.1'
    commonsIoVersion = '2.6'
    commonsLangVersion = '3.7'
    commonsRdfVersion = '0.5.0'
    commonsRdfJenaVersion = '0.6.0-SNAPSHOT'
    dropwizardVersion = '1.2.0'
    jacksonVersion = '2.9.5'
    javaxAnnotationsVersion = '1.3.2'
    javaxInjectVersion = '2.4.0'
    javaxJsonVersion = '1.1.2'
    jaxbVersion = '2.3.0'
    jaxrsVersion = '2.1'
    jettyAlpnServerVersion = '9.4.8.v20171121'
    jenaVersion = '3.8.0-SNAPSHOT'
    jerseyVersion = '2.25.1'
    jmsApiVersion = '2.0.1'
    jsonldVersion = '0.12.0'
    jwtVersion = '0.9.0'
    kafkaVersion = '1.0.0_1'
    metricsVersion = '4.0.2'
    mustacheVersion = '0.9.5_1'
    rabbitMqVersion = '5.2.0'
    slf4jVersion = '1.7.25'
    tamayaVersion = '0.3.0'
    tamayaVersion = '0.3-incubating'

    /* Transitive dependencies (for Karaf provisioning) */
    transitiveGuavaVersion = '20.0' // via mustache 0.9.5

    /* Transitive dependencies */
    commonsLoggingVersion = '1.2'
    geronimoJmsSpecVersion = '1.1.1'
    geronimoAnnotationSpecVersion = '1.0-alpha-1'
    hamcrestLibraryVersion = '1.3'
    jsr305Version = '3.0.2'
    servicemixXercesVersion = '2.11.0_1'
    thriftVersion = '0.11.0'
    xmlApisVersion = '1.4.01'

    /* Testing */
    apiguardianVersion = '1.0.0'
    awaitilityVersion = '3.1.0'
    bouncycastleVersion = '1.59'
    commonsTextVersion = '1.3'
    dexxCollectionVersion = '0.7'
    guavaVersion = '24.1-jre'
    httpclientVersion = '4.5.3'
    jsonVersion = '1.1.2'
    junitVersion = '5.1.1'
    junitPlatformVersion = '1.1.1'
    logbackVersion = '1.2.3'
    mockitoVersion = '2.18.3'

    /* OSGi */
    asmVersion = '6.1.1'
    jenaVersionRange = '[3.7,4)'
    karafVersion = '4.2.0'
    paxExamVersion = '4.11.0'
    projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
    spiflyVersion = '1.0.10'
    osgiEeJavaVersion= 'osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'
    // ActiveMQ requires Spring4, but Karaf 4.2 includes Spring5, so this forces an earlier spring dependency
    springFeatureVersion = '4.1.0'

    jacocoTestProjects = [
            'trellis-agent-jpms',
            'trellis-amqp-jpms',
            'trellis-api-jpms',
            'trellis-app-jpms',
            'trellis-app-triplestore-jpms',
            'trellis-audit-jpms',
            'trellis-file-jpms',
            'trellis-http-jpms',
            'trellis-id-jpms',
            'trellis-io-jpms',
            'trellis-jms-jpms',
            'trellis-kafka-jpms',
            'trellis-namespaces-jpms',
            //'trellis-osgi',
            'trellis-triplestore-jpms',
            'trellis-vocabulary-jpms',
            'trellis-webac-jpms',
    ]
}

allprojects { subproj ->

    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'jacoco'
    apply plugin: 'com.zyxist.chainsaw'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'idea'

    ext {
        vendor = 'Trellis LDP'
        homepage = 'https://www.trellisldp.org'
        docURL = 'https://trellis-ldp.github.io/trellis/apidocs/'
        license = 'Apache 2'
        jacocoVersion = '0.8.1'
        checkstyleVersion = '8.8'
    }

    jacoco.toolVersion = jacocoVersion

    group = 'org.trellisldp'

    repositories {
        mavenCentral()
        jcenter()
        flatDir {
            dirs "$rootProject.projectDir/lib"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    configurations {
        all {
            exclude group: 'org.trellisldp', module: 'trellis-api'
            exclude group: 'org.trellisldp', module: 'trellis-agent'
            exclude group: 'org.trellisldp', module: 'trellis-app'
            exclude group: 'org.trellisldp', module: 'trellis-audit'
            exclude group: 'org.trellisldp', module: 'trellis-constraint-rules'
            exclude group: 'org.trellisldp', module: 'trellis-event-serialization'
            exclude group: 'org.trellisldp', module: 'trellis-file'
            exclude group: 'org.trellisldp', module: 'trellis-http'
            exclude group: 'org.trellisldp', module: 'trellis-id'
            exclude group: 'org.trellisldp', module: 'trellis-io-jena'
            exclude group: 'org.trellisldp', module: 'trellis-jms'
            exclude group: 'org.trellisldp', module: 'trellis-kafka'
            exclude group: 'org.trellisldp', module: 'trellis-namespaces'
            exclude group: 'org.trellisldp', module: 'trellis-triplestore'
            exclude group: 'org.trellisldp', module: 'trellis-vocabulary'
            exclude group: 'org.trellisldp', module: 'trellis-webac'
            exclude group: 'xml-apis', module: 'xml-apis'
            exclude group: 'com.google.code.findbugs', module: 'jsr305'
            exclude group: 'org.apache.geronimo.specs', module: 'geronimo-annotation_1.2_spec'
            exclude group: 'commons-logging', module: 'commons-logging'
            exclude group: 'javax.inject', module: 'javax.inject'
            exclude group: 'org.apache.httpcomponents', module: 'httpclient-osgi'
            exclude group: 'org.apache.httpcomponents', module: 'httpcore-osgi'
        }
    }

    dependencies {
        implementation ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion") {force = true}
        implementation ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion") {force = true}
        implementation ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion") {force = true}
        implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion") {force = true}

        testImplementation ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion") {force = true}
        testImplementation ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion") {force = true}
        testImplementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion") {force = true}
        testImplementation ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion") {force = true}
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
        testImplementation("org.apiguardian:apiguardian-api:$apiguardianVersion")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    release {
        tagTemplate = '$name-$version'
        git {
            signTag = true
        }
    }
}

subprojects { subproj ->

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    jar {
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from sourceSets.main.allSource
    }

    task processConfig(type: Copy) {
        from('src/main/cfg') {
            include '**/*.cfg'
        }
        into 'build/cfg/main'
    }

    classes {
        classes.dependsOn processConfig
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    processResources {
        outputs.upToDateWhen { false }
        filesMatching(['**/features.xml', '**/banner.txt']) {
            expand project.properties
        }
    }

    signing {
        required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {
            def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
            def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                packaging 'jar'
                url 'https://www.trellisldp.org'
                inceptionYear '2017'
                name 'Trellis Linked Data Server'
                description 'The core components for a Trellis linked data server'
                organization {
                    name project.vendor
                    url project.homepage
                }

                developers {
                    developer {
                        id 'acoburn'
                        name 'Aaron Coburn'
                        email 'acoburn (at) apache (dot) org'
                        organization = 'Trellis LDP'
                        organizationUrl 'https://www.trellisldp.org'
                        roles {
                            role 'developer'
                        }
                        timezone '-5'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/trellis-ldp/trellis.git'
                    developerConnection 'scm:git:git@github.com/trellis-ldp/trellis.git'
                    url 'https://github.com/trellis-ldp/trellis'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        comments 'Copyright (c) 2017-2018 Trellis LDP'
                    }
                }
            }
        }
    }

    task mappings {
        doLast {
            println conf2ScopeMappings.mappings
        }
    }

    sonarqube {
        skipProject = JavaVersion.current().isJava10Compatible()
    }

    javadoc {
        doFirst {
            options.addStringOption('-module-path', classpath.asPath)
            options.addBooleanOption('html5', true)
        }
        outputs.upToDateWhen { false }
        source sourceSets.main.allJava
        classpath = files(sourceSets.main.compileClasspath)
        destinationDir = new File(projectDir, "docs/${version}")
        options {
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links 'https://trellis-ldp.github.io/trellis/apidocs/'
            links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.7/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://activemq.apache.org/maven/5.15.3/apidocs/'
            links 'https://jena.apache.org/documentation/javadoc/rdfconnection/'
            links 'https://kafka.apache.org/0100/javadoc/'
            links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
            links 'https://metrics.dropwizard.io/4.0.0/apidocs/'
            links 'https://www.dropwizard.io/1.3.0/dropwizard-core/apidocs/'
        }
    }

    afterEvaluate {
        if (subproj.name in jacocoTestProjects) {
            jacoco {
                applyTo subproj.tasks.matching { it.name == 'junitPlatformTest' }
            }
        }
    }
}

configure(rootProject) {

    task apidocs(type: Javadoc, dependsOn: getTasksByName('docs', true)) {
        outputs.upToDateWhen { false }
        destinationDir = new File(projectDir, "docs/apidocs")
        title = "Trellis Linked Data Server Documentation"
        exclude '**/impl/*'
        exclude '**/*Tests.java'
        options {
            memberLevel = JavadocMemberLevel.PUBLIC
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links 'https://trellis-ldp.github.io/trellis/apidocs/'
            links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.7/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://activemq.apache.org/maven/5.15.3/apidocs/'
            links 'https://jena.apache.org/documentation/javadoc/rdfconnection/'
            links 'https://kafka.apache.org/0100/javadoc/'
            links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
            links 'https://metrics.dropwizard.io/4.0.0/apidocs/'
            links 'https://www.dropwizard.io/1.3.0/dropwizard-core/apidocs/'
        }

        source subprojects.collect { project -> project.sourceSets.main.allJava }
        classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
    }

    sonarqube {
        properties {
            property "sonar.projectName", "Trellis Linked Data Server"
            property "sonar.projectKey", "org.trellisldp:trellis"
            property "sonar.links.homepage", "https://www.trellisldp.org"
            property "sonar.links.issue", "https://github.com/trellis-ldp/trellis/issues"
            property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis.git"
        }
    }

    // Ignore alpha, beta, milestone and release candidates
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject("Release Candidate")
                }
            }
        }
    }

    task jacocoMerge(type: JacocoMerge) {
        subprojects.findAll { it.name in jacocoTestProjects }
                .each { subproj ->
            executionData fileTree(dir: "${subproj.buildDir}/jacoco", include: '*.exec')
            dependsOn subproj.tasks.withType(Test)
        }
    }

    task jacocoRootReport(type: JacocoReport, dependsOn: jacocoMerge) {
        sourceDirectories = files(subprojects
                .findAll { it.name in jacocoTestProjects }
                .sourceSets.main.allSource.srcDirs)
        classDirectories = files(subprojects
                .findAll { it.name in jacocoTestProjects }
                .sourceSets.main.output)
        executionData jacocoMerge.destinationFile
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    coveralls {
        sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
        jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
    }

    tasks.coveralls {
        dependsOn 'jacocoRootReport'
    }

    configure(subprojects) { subproject ->
        task deleteSubprojectFiles(type: Delete) {
            delete "${projectDir}/settings.gradle", "${projectDir}/docs"
        }
    }
}