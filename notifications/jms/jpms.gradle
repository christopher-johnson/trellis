apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Notifications (JMS)'

ext {
    javaModuleName = 'org.trellisldp.jpms.jms'
    osgiModuleName = 'org.trellisldp.jms'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['org.trellisldp.jpms.vocabulary']
    hacks {
        patches('org.apache.geronimo.specs:geronimo-jms_1.1_spec', 'javax.jms:javax.jms-api')
    }
}

dependencies {
    patch "org.apache.geronimo.specs:geronimo-jms_1.1_spec:$geronimoJmsSpecVersion"

    api("javax.jms:javax.jms-api:$jmsApiVersion")
    api("org.glassfish.hk2.external:javax.inject:$javaxInjectVersion")
    api("org.trellisldp:trellis-api-jpms:$version")

    implementation("org.apache.tamaya:tamaya-api:$tamayaVersion")
    implementation("org.slf4j:slf4j-api:$slf4jVersion")

    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("javax.annotation:javax.annotation-api:$javaxAnnotationsVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
    testImplementation("org.apache.tamaya:tamaya-core:$tamayaVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation("org.trellisldp:trellis-event-serialization-jpms:$version")
    testImplementation("org.trellisldp:trellis-vocabulary-jpms:$version")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$osgiModuleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",osgi.serviceloader;
                filter:="(osgi.serviceloader=org.trellisldp.api.ActivityStreamService)";resolution:=mandatory; cardinality:=mandatory,osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.10))'''
    }
}