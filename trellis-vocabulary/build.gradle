apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Trellis Vocabularies'

ext {
    moduleName = 'org.trellisldp.vocabulary'
}

javaModule {
    name = 'org.trellisldp.vocabulary'
    extraTestModules = ['org.mockito']
    hacks {
        patches ('org.apache.httpcomponents:httpclient-osgi', 'org.apache.httpcomponents:httpclient')
        patches ('org.apache.httpcomponents:httpcore-osgi', 'org.apache.httpcomponents:httpcore')
        patches ('xerces:xercesImpl', 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces')
    }
}

configurations {
    all {
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

dependencies {
    patch "org.apache.httpcomponents:httpclient-osgi:$httpClientOsgiVersion"
    patch "org.apache.httpcomponents:httpcore-osgi:$httpCoreOsgiVersion"
    patch "org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces:$servicemixXercesVersion"

    api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion

    testImplementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
    testImplementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: apiguardianVersion
    testImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: httpClientOsgiVersion
    testImplementation group: 'org.apache.httpcomponents', name: 'httpclient-cache', version: httpClientOsgiVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'xerces', name: 'xercesImpl', version: xercesVersion

    testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
        instruction 'Require-Capability', '''\
            osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.apache.commons.rdf.api.RDF)";
                resolution:=mandatory; cardinality:=at_least_one
            '''
    }
}
